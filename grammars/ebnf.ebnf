grammar = { rule };
rule  = identifier, '=', definition, {'|', definition} ';';
definition = term, {',', term};
term = factor, ['-', exception];
exception = factor;
factor = [integer, '*'], primary;
primary = option
        | repetition
        | group
        | special
        | string
        | identifier
        | empty;
option = '[', definitions, ']';
repetition = '{', definitions, '}';
group = '(', definitions, ')';
special = '?', {character - '?'}, '?';
string = "'", character - "'", {character - "'"}, "'"
       | '"', character - '"', {character - '"'}, '"';
identifier = letter, {letter | digit | "_" };
empty = ;
integer = digit, {digit};
comment = "(*", {character} , "*)";
character = letter | digit | symbol | whitespace;
letter = "A" | "B" | "C" | "D" | "E" | "F" | "G"
       | "H" | "I" | "J" | "K" | "L" | "M" | "N"
       | "O" | "P" | "Q" | "R" | "S" | "T" | "U"
       | "V" | "W" | "X" | "Y" | "Z" | "a" | "b"
       | "c" | "d" | "e" | "f" | "g" | "h" | "i"
       | "j" | "k" | "l" | "m" | "n" | "o" | "p"
       | "q" | "r" | "s" | "t" | "u" | "v" | "w"
       | "x" | "y" | "z" ;
digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
symbol = "[" | "]" | "{" | "}" | "(" | ")" | "<" | ">"
       | "'" | '"' | "=" | "|" | "." | "," | ";" | "_" ;
whitespace = " " | "\n" | "\t"
